buildscript {
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:3.0.1"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
    classpath "com.github.dcendents:android-maven-gradle-plugin:2.0"
    classpath "digital.wup:android-maven-publish:3.1.1"
  }
}

// Populate deployment credentials in an environment-aware fashion.
//
// * Local development:
//      Stored in local.properties file on the machine
// * CI Server:
//      Stored in environment variables before launch
Properties properties = new Properties()

def credentialsFile = new File(project.rootDir, "local.properties")
if (credentialsFile.exists()) {
  credentialsFile.withReader { properties.load(it) }
}

def bintrayUser = properties.getProperty("BINTRAY_USER", System.getenv("bintrayUser"))
def bintrayKey = properties.getProperty("BINTRAY_KEY", System.getenv("bintrayKey"))
def sonatypeUser = properties.getProperty("SONATYPE_USER", System.getenv("sonatypeUser"))
def sonatypePass = properties.getProperty("SONATYPE_PASS", System.getenv("sonatypePass"))

allprojects {
  repositories {
    google()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }

  // Store deployment credentials
  ext.bintrayUser = bintrayUser
  ext.bintrayKey = bintrayKey
  ext.sonatypeUser = sonatypeUser
  ext.sonatypePass = sonatypePass
}


task clean(type: Delete) {
  delete rootProject.buildDir
}
